plugins {
    id 'java'
    id 'findbugs'
    id 'pmd'
    id 'jacoco'
    id 'signing'
    id 'maven'
}

/*** Project Properties ***/

/*
 * Notes
 * ===================================================
 * 1) Project.name is already reserved for the name of
 *    the project directory. Thus adding an extra
 *    property, ext.name, for project name.
 * 2) By default, version number has the "-SNAPSHOT"
 *    suffix. The "-SNAPSHOT" suffix is required for
 *    for publishing to the snapshot repo. To publish
 *    to the staging repo, set the staging property
 *    to "true".
 */

group = 'org.computelab'
ext.name = 'config'
version = '0.1.0'
version = staging ? version : version + '-SNAPSHOT'

/*** Java ***/

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:' + gson
    compile 'com.google.guava:guava:' + guava
    // Logging
    compile 'org.slf4j:slf4j-api:' + slf4j
    // Testing
    testCompile 'junit:junit:' + junit
    testCompile 'org.mockito:mockito-core:' + mockito
    testCompile 'org.apache.commons:commons-lang3:' + commons
    testRuntime 'ch.qos.logback:logback-core:' + logback
    testRuntime 'ch.qos.logback:logback-classic:' + logback
}

jar {
    group = project.group
    baseName = project.ext.name
    version = project.version
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

/*** Code Quality Tools ***/

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

/*** Signing ***/

signing {
    sign configurations.archives
}

/*** Maven Publishing ***/

ext.stagingRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
ext.snapshotRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: project.ext.stagingRepoUrl) {
                authentication(
                        userName: "${System.env.OSSRH_USR}",
                        password: "${System.env.OSSRH_PWD}"
                )
            }

            snapshotRepository(url: project.ext.snapshotRepoUrl) {
                authentication(
                        userName: "${System.env.OSSRH_USR}",
                        password: "${System.env.OSSRH_PWD}"
                )
            }

            pom.project {
                name 'config'
                description 'Configuration in plain Java'
                url 'https://github.com/computelab/config'
                scm {
                    url 'https://github.com/computelab/config.git'
                }
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'neurite'
                    }
                }
            }
        }
    }
}
