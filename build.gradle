plugins {
    id 'java'
    id 'findbugs'
    id 'pmd'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
}

/*** Project ***/

group = 'org.computelab'
ext.name = 'config'
version = '0.1.0-SNAPSHOT'

/*** Java ***/

jar {
    group = project.group
    baseName = project.ext.name
    version = project.version
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:' + gson
    compile 'com.google.guava:guava:' + guava
    // Logging
    compile 'org.slf4j:slf4j-api:' + slf4j
    // Testing
    testCompile 'junit:junit:' + junit
    testCompile 'org.mockito:mockito-core:' + mockito
    testCompile 'org.apache.commons:commons-lang3:' + commons
    testRuntime 'ch.qos.logback:logback-core:' + logback
    testRuntime 'ch.qos.logback:logback-classic:' + logback
}

/*** Code Quality Tools ***/

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

/*** Signing ***/

signing {
    sign configurations.archives
}

/*** Maven Publishing ***/

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url OSSRH_URL
            credentials {
                username OSSRH_USR
                password OSSRH_PWD
            }
        }
    }
}
